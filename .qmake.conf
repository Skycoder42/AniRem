VERSION = 2.0.0

CONFIG += warning_clean exceptions
android|clang: CONFIG += c++14
else: CONFIG += c++17
DEFINES += QT_DEPRECATED_WARNINGS QT_ASCII_CAST_WARNINGS QT_USE_QSTRINGBUILDER

QMAKE_TARGET_COMPANY = "Skycoder42"
QMAKE_TARGET_DESCRIPTION = "Anime season reminder"
QMAKE_TARGET_COPYRIGHT = "Felix Barz"
QMAKE_TARGET_BUNDLE_PREFIX = de.skycoder42

DEFINES += "VERSION=\\\"$$VERSION\\\""
DEFINES += "COMPANY=\"\\\"$$QMAKE_TARGET_COMPANY\\\"\""
DEFINES += "BUNDLE=\"\\\"$$QMAKE_TARGET_BUNDLE_PREFIX\\\"\""

# installing
exists($$PWD/install.pri):include($$PWD/install.pri)

android: INSTALL_TRANSLATIONS = /assets/translations
else:create_installer {
	CONFIG += no_quick
	QTIFW_PKG_ROOT = /packages/de.skycoder42.anirem
	INSTALL_PREFIX = $$QTIFW_PKG_ROOT/data
	INSTALL_BINS = $$INSTALL_PREFIX
	win32: INSTALL_LIBS = $$INSTALL_BINS
}

isEmpty(INSTALL_PREFIX) {
	INSTALL_PREFIX = $$[QT_INSTALL_PREFIX]
	isEmpty(INSTALL_BINS): INSTALL_BINS = $$[QT_INSTALL_BINS]
	isEmpty(INSTALL_LIBS): INSTALL_LIBS = $$[QT_INSTALL_LIBS]
	isEmpty(INSTALL_HEADERS): INSTALL_HEADERS = $$[QT_INSTALL_HEADERS]
	isEmpty(INSTALL_PLUGINS): INSTALL_PLUGINS = $$[QT_INSTALL_PLUGINS]
	isEmpty(INSTALL_TRANSLATIONS): INSTALL_TRANSLATIONS = $$[QT_INSTALL_TRANSLATIONS]
} else:mac {
	APP_PREFIX = Ani-Rem.app/Contents
	isEmpty(INSTALL_APP): INSTALL_BINS = $${INSTALL_PREFIX}
	isEmpty(INSTALL_BINS): INSTALL_BINS = $${INSTALL_PREFIX}/$${APP_PREFIX}/MacOS
	isEmpty(INSTALL_LIBS): INSTALL_LIBS = $${INSTALL_PREFIX}/$${APP_PREFIX}/Frameworks
	isEmpty(INSTALL_HEADERS): INSTALL_HEADERS = $${INSTALL_PREFIX}/$${APP_PREFIX}/Headers
	isEmpty(INSTALL_PLUGINS): INSTALL_PLUGINS = $${INSTALL_PREFIX}/$${APP_PREFIX}/PlugIns
	isEmpty(INSTALL_TRANSLATIONS): INSTALL_TRANSLATIONS = $${INSTALL_PREFIX}/$${APP_PREFIX}/Resources/translations
	isEmpty(INSTALL_SHARE): INSTALL_SHARE = $${INSTALL_PREFIX}/$${APP_PREFIX}/Resources
} else {
	isEmpty(INSTALL_BINS): INSTALL_BINS = $${INSTALL_PREFIX}/bin
	isEmpty(INSTALL_LIBS): INSTALL_LIBS = $${INSTALL_PREFIX}/lib
	isEmpty(INSTALL_HEADERS): INSTALL_HEADERS = $${INSTALL_PREFIX}/include
	isEmpty(INSTALL_PLUGINS): INSTALL_PLUGINS = $${INSTALL_PREFIX}/plugins
	isEmpty(INSTALL_TRANSLATIONS): INSTALL_TRANSLATIONS = $${INSTALL_PREFIX}/translations
}
isEmpty(INSTALL_SHARE): INSTALL_SHARE = $${INSTALL_PREFIX}/share
